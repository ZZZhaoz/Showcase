// Set the pixel color to an interesting procedural color generated by mixing
// and filtering Perlin noise of different frequencies.
//
// Uniforms:
uniform mat4 view;
uniform mat4 proj;
uniform float animation_seconds;
uniform bool is_moon;
// Inputs:
in vec3 sphere_fs_in;
in vec3 normal_fs_in;
in vec4 pos_fs_in; 
in vec4 view_pos_fs_in; 
// Outputs:
out vec3 color;
#define PI 3.14159265358979323846

// expects: blinn_phong, perlin_noise
void main()
{
  /////////////////////////////////////////////////////////////////////////////
  float theta = 0.25 * PI * animation_seconds;
  vec4 light = view * vec4(4 * cos(theta), 4, 4 * sin(theta), 1.0);

  vec3 ka = vec3(0.02);
  vec3 kd = is_moon ? vec3(0.82, 0.75, 0.78) : vec3(0.28, 0.40, 0.89);
  vec3 ks = vec3(0.8);
  float p  = is_moon ? 600.0 : 300.0;

  vec3 n = normalize(normal_fs_in);
  vec3 v = -normalize(view_pos_fs_in.xyz);
  vec3 l = normalize(light.xyz - view_pos_fs_in.xyz);

  float freq = 6.0;
  float f1 = perlin_noise(freq * sphere_fs_in);
  float f2 = perlin_noise(2.0 * freq * sphere_fs_in);
  float f3 = perlin_noise(4.0 * freq * sphere_fs_in);
  float f4 = perlin_noise(8.0 * freq * sphere_fs_in);


  float turbulence = abs(f1 + 0.5*f2 + 0.25*f3 + 0.125*f4);
  turbulence = clamp(turbulence, 0.0, 1.0);

  float pattern = 0.5 + 0.5 * sin(6.0 * sphere_fs_in.x + 5.0 * turbulence);
  pattern = clamp(pattern, 0.0, 1.0);

  vec3 base_color;
  if (is_moon)
    base_color = mix(vec3(0.75, 0.75, 0.78), vec3(0.35, 0.35, 0.45), pattern);
  else
    base_color = mix(vec3(0.12, 0.25, 0.85), vec3(0.35, 0.55, 0.95), pattern);

  color = blinn_phong(ka, base_color, ks, p, n, v, l);

  
  /////////////////////////////////////////////////////////////////////////////
}
